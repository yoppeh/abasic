10 cls
15 print "             POLITICS"
20 print "BY JEFF PARKES"
30 print  : print "IN THIS GAME YOU ARE THE MAYOR OF" : print "A SMALL TOWN WITH A POPULAR DOGCATCHER"
35 print "YOU MUST DECIDE HOW TO SPEND YOUR" : print "BUDGET TO DEAL WITH PROBLEMS THAT COME" : print "UP."
40 print "YOUR OBJECT IS TO GET RE-ELECTED"
45 B = 100000 : T = 0 : N = 0
46 print  : print "YOU START WITH $"; B; " IN YOUR BUDGET"
47 print "PRESS [ENTER] TO BEGIN...";  : input enter
50 cls : print "BUDGET: $"; B : if B <= 0 then 500
55 T = T + 1 : if T > 10 then 300
60 X = int(rnd(1) * 22) + 1 : 
65 for I = 1 to X : read P$,C : next I
70 print  : print P$ : print  : print "ESTIMATED COST: $"; C
75 print  : print "DO YOU WANT TO SPEND THE MONEY [y/n]: ";  : input K$
77 if K$ = "y" then 100
78 if K$ = "n" then 80
79 print "You entered ["; K$; "] PLEASE enter [y] OR [n]" : goto 75
80 N = N + 1 : if N > rnd(1) * 6 + 4 then 400
90 if rnd(1) > 0.92 then gosub 200
95 print "PRESS [ENTER]";  : input enter : goto 50
100 A = int(rnd(1) * (C / 10)) : if rnd(1) > 0.7 then A = -A
105 C = C + A : print "ACTUAL COST:$"; C : B = B - C
110 if rnd(1) > 0.49 then gosub 200
111 print "PRESS ENTER";  : input enter
120 goto 50
200 print "VOTERS WANT TO INCREASE YOUR BUDGET!"
205 E = int(rnd(1) * 1000) + 500 : B = B + E
210 return
300 cls
310 print "ELECTIONS! IT'S YOU AGAINST THE" : print "DOGCATCHER!"
315 print "VOTES:" : YV = int(rnd(1) * 1000 + 500) + 15 * (20 - N) : OV = 2000 - YV
320 print  : print "FOR YOU:"; YV : print "FOR DOGCATCHER:"; OV
325 print  : if OV < YV then print "YOU DID IT! NICE GOIN' MAN!" : goto 600
330 if OV = YV then print "DARN. TIE. LETS T201"
335 print "WELL, IT LOOKS LIKE YOU'VE LOST."
336 print "NOW, YOU HAVE TO PLAY MOUSE TRAP" : print "WITH ME!!! HA HA HA!!!" : goto 600
400 cls
410 print "CITIZENS DECIDE TO KICK YOU OUT OF" : print "OFFICE!"
420 for I = 1 to 1000 : next I
430 print "THE VOTERS MAY NOT LIKE YOU AS MAYOR,"
435 print "BUT THEY FIND YOU A NEW JOB."
440 print "EVERYONE THINKS YOU'RE A TERRIFIC" : print "DOGCATCHER!" : goto 600
500 print "THERE'S NO MONEY LEFT!"
510 print "THE CITIZENS KICK YOU OUT AND ELECT" : print "THE DOGCATCHER MAYOR."
520 print "YOU REALLY SCREWED UP THIS TIME!" : goto 606
600 print  : print "YOU HAVE $"; B; " LEFT"
605 if B > 0 then print "PLAY AGAIN (Y/N/G)";  : goto 610
606 print "PLAY AGAIN [y]/[n]: "; 
610 input K$ : if K$ = "y" then cls : goto 45
620 if K$ = "g" and B > 0 then 650
630 if K$ = "n" then print "Thanks for playing!" : end
640 print "Invalid input: ["; K$; "], try again" : goto 605
650 print "LUCKILY YOU MANAGED TO GRAB WHAT WAS" : print "LEFT IN THE BUDGET"
655 print "AND ESCAPE TO THE SUNNY CARRIBEAN" : print "ISLANDS" : B = 0 : goto 600
660 end
700 data THE LITTLE LEAGUE NEED NEW UNIFORMS,500
705 data A FLOOD HITS TOWN! REPAIR NEEDED,20000
710 data THE FIRE HOUSE BURNED DOWN! NEED NEW ONE,30000
715 data A TRUCK RAMMED INTO CITY HALL! REPAIRS ARE NECESSARY,5000
720 data THE TOWN MAINFRAME BROKE DOWN! NEED A REPAIRMAN,1000
725 data CITIZENS WANT A STATUE BUILT TO HONOR THE TOWN DOGCATCHER,15000
730 data CITIZENS DEMAND CITY PARK,50000
735 data DOGCATCHER NEEDS NEW TRUCK & MORE NETS,10000
740 data LIBRARY NEEDS TO REPLACE LOST BOOKS,8000
745 data THE POLICE FORCE NEEDS CRUISERS,45000
750 data CITIZENS DEMAND REPAIRS ON POTHOLE-RIDDEN STREETS,15000
755 data DOGCATCHER GOES ON STRIKE TO DEMAND MORE PAY,5000
760 data CITIZENS WANT TREES TRIMMED ON PUBLIC STREETS,1000
765 data POLICE WANT TO TRAIN NEW OFFICERS IN WATER BALLET,10000
770 data THE FIRE DEPARTMENT WANTS TO REPAINT HOOKS AND LADDERS,1500
775 data THE CITY DUMP WANTS TO BUY A NEW TRASH COMPACTER,20000
780 data THE ROADS NEED REPAVING,10000
785 data THE STREET LAMPS ARE ALL BURNED OUT,900
790 data CITIZENS WANT STREET SIGNS PUT UP AT EVERY CORNER,15000
795 data TOWN OVERRUN WITH RODENTS! CITIZENS DEMAND YOU GET RID OF THEM,35000
800 data THE DOGCATCHER WANTS A VACATION IN HAWAII,5000
810 data STUDENTS IN CAFETERIA DEMAND BETTER FOOD,10000
